plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-rc2'
}

group = 'me.verschuls.tren'
version = '1.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.papermc.io/repository/maven-public/"}
    maven { url "https://repo.codemc.io/repository/maven-releases/"}
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    implementation("me.verschuls.cbu:configlibutil:1.0")
    compileOnly("org.snakeyaml:snakeyaml-engine:2.7")
    compileOnly("io.lettuce:lettuce-core:7.0.0.RELEASE")
    compileOnly("org.lz4:lz4-java:1.8.0")
    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    archiveFileName.set("${project.name}-${version}.jar")

    def prefix = project.group+".libs"
    ["org.jetbrains.annotations", "org.jdbi",
     "de.exlll.configlib", "me.verschuls.cbu"]
            .forEach { pack ->
                def split = pack.split("\\.");
                def name = split[split.length-1];
                relocate(pack, "$prefix.$name")
            }

    exclude(
            "module-info.class",
            "META-INF/*.RSA",
            "META-INF/*.DSA",
            "META-INF/*.SF",
            "META-INF/LICENSE*",
            "META-INF/NOTICE*",
            "META-INF/DEPENDENCIES",
            "META-INF/maven/**",
            "javax/**",
            "LICENSE*",
            "AUTHORS*",
            "README*",
            "*.html",
            //"*.txt",
            "org/snakeyaml/**",
            "org/yaml/**"
    )
    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
    }
    mergeServiceFiles()
    minimize()
}

build {
    dependsOn shadowJar
}
